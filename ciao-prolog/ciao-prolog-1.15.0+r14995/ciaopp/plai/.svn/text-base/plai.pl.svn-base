:- module(plai,
	[ plai/5,
	  cleanup_plai/1,
	  mod_plai/5,
	  %% *** Needs revising MH
          is_checker/1,
	  analyze/7,       %% JNL
	  init_fixpoint/1, %% JNL	  
	  entry_point/5    %% JNL
	],
	[ assertions
%	,.(debug)
	]).

% Ciao library
:- use_module(library(counters), [setcounter/2, inccounter/2]).
:- use_module(library(prolog_sys), [statistics/2]).
:- use_module(library(terms_vars), [varset/2]).
:- use_module(library(vndict), [vars_names_dict/3]).
%% *** MH
:- use_module(library(lists), [append/3]).
:- use_module(library(aggregates)).
% CiaoPP library
:- use_module(spec(s_simpspec), [make_atom/2]).
:- use_module(spec(sp_clauses), [init_unfold/0]).
:- use_module(spec(unfold_times), 
	[ init_unfold_times/0, 
	  ask_unfold_times/1, 
	  global_time_ellapsed/3 ]).
:- use_module(program(p_unit), 
	[ entry_assertion/3, type_of_goal/2, type_of_directive/2 ]).
:- use_module(plai(entry_policy), [get_entry_info/3]).
:- use_module(ciaopp(preprocess_flags), [current_pp_flag/2,
	push_pp_flag/2, pop_pp_flag/1]).
:- use_module(spec(mem_usage), 
	[reset_mem_usage/0, ask_mem_usage/2]).

%:- use_package(spec(nomem_usage)).

% Plai library
:- use_module(plai(domains), 
	[ init_abstract_domain/2, empty_entry/3, unknown_entry/3, 
	  unknown_entry/4, info_to_asub/5,dom_statistics/2]).
:- use_module(plai(normalize), [normalize_delay/4]).
:- use_module(plai(plai_errors), [undo_errors/0]).
:- use_module(plai(fixpo_plai), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(plai(fixpo_dd), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(plai(fixpo_di), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(plai(fixpo_check_di), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(plai(fixpo_check_di2), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(plai(fixpo_check_di3), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(plai(fixpo_check_di4), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(plai(fixpo_check_di5), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(plai(fixpo_check_reduced_di), [query/8, init_fixpoint/0, cleanup_fixpoint/1]).
:- use_module(poly_spec(heuristic_pcpe), [query/1, init_fixpoint/0, cleanup_fixpoint/0]).
:- use_module(plai(fixpo_bu), [tp/1, init_fixpoint/0, cleanup_fixpoint/1]).

:- use_module(plai(tarjan), [tarjan/2, recursive_classify/4, fake_recursive_classify/2]).
:- use_module(plai(transform), 
	[ cleanup_clauses/0,transform_clauses/5,determine_r_flag/3]).
:- use_module(plai(fixpo_ops), [
	store_previous_analysis/1,
	store_previous_analysis_completes/1,
	reset_previous_analysis/1]).

:- use_module(plai(output_java_info), [java_statistics/1]).
%------------------------------------------------------------------------%

:- doc(title,"Program Analysis (PLAI)").

:- doc(bug,"Call patterns generated for imported predicates should be 
	saved in the related modules .asr files after analysis.").
:- doc(bug,"Meta-preds should be analysed as such only if imported.
        Otherwise, there is the code... and it may be recursive!").
:- doc(bug,"An export from a reexport yields unknown and bottom.").

%------------------------------------------------------------------------%

:- doc(cleanup_plai(AbsInt),"Cleanups the database of analysis
	of all permanent information regarding abstract
	domain @var{AbsInt}.").

cleanup_plai(AbsInt):-
	fixpo_plai:cleanup_fixpoint(AbsInt),
	fixpo_dd:cleanup_fixpoint(AbsInt),
	fixpo_di:cleanup_fixpoint(AbsInt),
	fixpo_check_di:cleanup_fixpoint(AbsInt),
	fixpo_check_di2:cleanup_fixpoint(AbsInt),
	fixpo_check_di3:cleanup_fixpoint(AbsInt),
	fixpo_check_reduced_di:cleanup_fixpoint(AbsInt),
	fixpo_check_di4:cleanup_fixpoint(AbsInt),
	fixpo_check_di5:cleanup_fixpoint(AbsInt),
	fixpo_bu:cleanup_fixpoint(AbsInt),
	heuristic_pcpe:cleanup_fixpoint.


init_fixpoint(plai):- fixpo_plai:init_fixpoint.
init_fixpoint(dd):- fixpo_dd:init_fixpoint.
init_fixpoint(di):- fixpo_di:init_fixpoint.
init_fixpoint(check_di):- fixpo_check_di:init_fixpoint.
init_fixpoint(check_di2):- fixpo_check_di2:init_fixpoint.
init_fixpoint(check_di3):- fixpo_check_di3:init_fixpoint.
init_fixpoint(check_reduc_di):- fixpo_check_reduced_di:init_fixpoint.
init_fixpoint(check_di4):- fixpo_check_di4:init_fixpoint.
init_fixpoint(check_di5):- fixpo_check_di5:init_fixpoint.
init_fixpoint(bu):- fixpo_bu:init_fixpoint.
init_fixpoint(poly_spec):- heuristic_pcpe:init_fixpoint.

:- doc(plai(Cls,Ds,Fixp,AbsInt,Info),"Performs the analysis of the
	clauses in @var{Cls} (dictionaries of variables in @var{Ds})
	with fixpoint algorithm @var{Fixp} and analysis domain
	@var{AbsInt}.  The analysis results are left in a permanent
	database (see @lib{plai_db}). @var{Info} is a list of
	properties and informations. One possible value is
	@tt{[time(Total,[(subtask1,T1),...,(subtaskN,TN)])]}.").

plai(Cls,Ds,Fixp,AbsInt,[TimeInfo,MemoryInfo|Info]):-
	statistics(runtime,_),
	% initialization
	init_abstract_domain(AbsInt,Norm),
	init_fixpoint(Fixp),	
	init_unfold,
	init_unfold_times,
	cleanup_clauses,
	undo_errors,
	preprocess(Fixp,Norm,AbsInt,Cls,Ds,Ps),
	statistics(runtime,[GT1,T1]),
	display_list(['{preprocessed for plai in ',T1, ' msec.}\n']),
	reset_mem_usage,
	topdown_analysis(Fixp,AbsInt,Ps),
	%% don't upload -JNL
	%statistics,
	pop_pp_flag(variants),
	statistics(runtime,[GT,_T]),
	global_time_ellapsed(GT,GT1,T),
	Total is T1 + T,
	current_pp_flag(local_control,LC),
	(  is_checker(Fixp)
	-> Header = 'certificate checked by'
	;  Header = 'analyzed by'),
        display_list(['{', Header, ' ', Fixp, ' using ', AbsInt, 
                     ' with local-control ', LC,' in ', T, ' msec.}\n']),
	TimeInfo = time(Total,[(prep,T1),(ana,T)|Local_C_Info]),
	java_statistics(AbsInt),
	ask_mem_usage(Delta,Details),
	MemoryInfo = memory(Delta,Details),
	ask_unfold_times(Local_C_Info),
        dom_statistics(AbsInt, Info).

%% *** This has to be revised. MH
is_checker(check_di3) :- !.
is_checker(X) :- 
	atom_codes(X,Xs), 
	append("check",_,Xs).

preprocess(di,Norm,AbsInt,Cls,Ds,Ps):-!,
	current_pp_flag(local_control,LC),
	( LC == df_hom_emb_as ->
	    tarjan(Cls,Sccs),
	    recursive_classify(Cls,Sccs,Rs,Ps)
	;
	    fake_recursive_classify(Cls,Rs), 
	    Ps = notarjan
	),
	transform_clauses_(Norm,Cls,Ds,Rs,Ps,AbsInt).
preprocess(Fixp,Norm,AbsInt,Cls,Ds,Ps):-
	( Fixp == check_di ; Fixp == check_di2 ; Fixp == check_di3 ; Fixp == check_reduc_di), !,
	% check_di doesn't need tarjan 
	fake_recursive_classify(Cls,Rs), 
	Ps = notarjan,
	transform_clauses_(Norm,Cls,Ds,Rs,Ps,AbsInt),
	reset_previous_analysis(AbsInt),
	store_previous_analysis_completes(AbsInt),
	( Fixp == check_di ->
	  fixpo_check_di:cleanup_fixpoint(AbsInt)
	;
	    ( Fixp == check_di2 ->
	      fixpo_check_di2:cleanup_fixpoint(AbsInt)
	    ;
		(Fixp == check_reduc_di ->
		 fixpo_check_reduced_di:cleanup_fixpoint(AbsInt)
		 ;
		    fixpo_check_di3:cleanup_fixpoint(AbsInt)
		)
	    )
	).
preprocess(check_di4,Norm,AbsInt,Cls,Ds,Ps):-!,
	% check_di doesn't need tarjan 
	fake_recursive_classify(Cls,Rs), 
	Ps = notarjan,
	transform_clauses_(Norm,Cls,Ds,Rs,Ps,AbsInt),
	store_previous_analysis(AbsInt),
	fixpo_check_di4:cleanup_fixpoint(AbsInt).
preprocess(check_di5,Norm,AbsInt,Cls,Ds,Ps):-!,
	% check_di doesn't need tarjan 
	fake_recursive_classify(Cls,Rs), 
	Ps = notarjan,
	transform_clauses_(Norm,Cls,Ds,Rs,Ps,AbsInt),
	store_previous_analysis(AbsInt),
	fixpo_check_di5:cleanup_fixpoint(AbsInt).
preprocess(poly_spec,_Norm,_AbsInt,_Cls,_Ds,_Ps):-!,
	heuristic_pcpe:cleanup_fixpoint.
preprocess(Fixp,Norm,AbsInt,Cls,Ds,Ps):-
	( Fixp == plai ; Fixp == dd),
	tarjan(Cls,Sccs),
	recursive_classify(Cls,Sccs,Rs,Ps), !,
	transform_clauses_(Norm,Cls,Ds,Rs,Ps,AbsInt).
preprocess(bu,Norm,AbsInt,Cls,Ds,Ps):-
	tarjan(Cls,Sccs),
	recursive_classify(Cls,Sccs,Rs,Ps), !,
	transform_clauses_(Norm,Cls,Ds,Rs,Ps,AbsInt).
% preprocess(bu,Norm,AbsInt,Cls,Ds,Ps):-!,
% 	fake_recursive_classify(Cls,Rs),
% 	Ps = notarjan,
% 	transform_clauses_(Norm,Cls,Ds,Rs,Ps,AbsInt).
preprocess(delay,_Norm,AbsInt,Cls,Ds,[]):-
	normalize_delay(Cls,Ds,NormCls,NormDs),
	transform_clauses(NormCls,NormDs,[],[],AbsInt).

transform_clauses_(off,Cls,Ds,Rs,Ps,AbsInt):- !,
	transform_clauses(Cls,Ds,Rs,Ps,AbsInt).
transform_clauses_(_Norm,Cls,Ds,Rs,Ps,AbsInt):-
	normalize_delay(Cls,Ds,NormCls,NormDs),
	transform_clauses(NormCls,NormDs,Rs,Ps,AbsInt).

topdown_analysis(poly_spec,_AbsInt,_Ps):-!,
	findall(Goal,
	        entry_point(_AbsInt,Goal,_Gv,_Call,_Name),
		Goals),
	heuristic_pcpe:query(Goals).
topdown_analysis(bu,AbsInt,_):-
	tp(AbsInt).
topdown_analysis(Fixp,AbsInt,Ps):-
	entry_point(AbsInt,Goal,Gv,Call,Name),
	( analyze(Fixp,AbsInt,Ps,Goal,Gv,Call,Name) -> true ),
	fail.
topdown_analysis(_Fixp,_AbsInt,_Ps).

analyze(plai,AbsInt,Ps,Goal,Gv,Call,Name):-
	functor(Goal,F,A),
	make_atom([F,A],K),
	determine_r_flag(Ps,F/A,RFlag),
	fixpo_plai:query(AbsInt,K,Goal,Gv,RFlag,Name,Call,_Succ).
analyze(dd,AbsInt,Ps,Goal,Gv,Call,Name):-
	functor(Goal,F,A),
	make_atom([F,A],K),
	determine_r_flag(Ps,F/A,RFlag),
	fixpo_dd:query(AbsInt,K,Goal,Gv,RFlag,Name,Call,_Succ).
analyze(di,AbsInt,_Ps,Goal,Gv,Call,Name):-
	functor(Goal,F,A),
	make_atom([F,A],K),
	fixpo_di:query(AbsInt,K,Goal,Gv,_,Name,Call,_Succ).
analyze(check_di,AbsInt,_Ps,Goal,Gv,Call,Name):-
	push_pp_flag(reuse_fixp_id,on),
	functor(Goal,F,A),
	make_atom([F,A],K),
	fixpo_check_di:query(AbsInt,K,Goal,Gv,_,Name,Call,_Succ),
	pop_pp_flag(reuse_fixp_id).
analyze(check_di2,AbsInt,_Ps,Goal,Gv,Call,Name):-
	push_pp_flag(reuse_fixp_id,on),
	functor(Goal,F,A),
	make_atom([F,A],K),
	fixpo_check_di2:query(AbsInt,K,Goal,Gv,_,Name,Call,_Succ),
	pop_pp_flag(reuse_fixp_id).
analyze(check_di3,AbsInt,_Ps,Goal,Gv,Call,Name):-
	push_pp_flag(reuse_fixp_id,on),
	functor(Goal,F,A),
	make_atom([F,A],K),
	fixpo_check_di3:query(AbsInt,K,Goal,Gv,_,Name,Call,_Succ),
	pop_pp_flag(reuse_fixp_id).
analyze(check_reduc_di,AbsInt,_Ps,Goal,Gv,Call,Name):-
	push_pp_flag(reuse_fixp_id,on),
	functor(Goal,F,A),
	make_atom([F,A],K),
	fixpo_check_reduced_di:query(AbsInt,K,Goal,Gv,_,Name,Call,_Succ),
	pop_pp_flag(reuse_fixp_id).
analyze(check_di4,AbsInt,_Ps,Goal,Gv,Call,Name):-
	push_pp_flag(reuse_fixp_id,on),
%	push_pp_flag(widencall,off),
	functor(Goal,F,A),
	make_atom([F,A],K),
	fixpo_check_di4:query(AbsInt,K,Goal,Gv,_,Name,Call,_Succ),
%	pop_pp_flag(widencall),
	pop_pp_flag(reuse_fixp_id).
analyze(check_di5,AbsInt,_Ps,Goal,Gv,Call,Name):-
	push_pp_flag(reuse_fixp_id,on),
	functor(Goal,F,A),
	make_atom([F,A],K),
	fixpo_check_di5:query(AbsInt,K,Goal,Gv,_,Name,Call,_Succ),
	pop_pp_flag(reuse_fixp_id).

entry_point(AbsInt,Goal,Qv,Call,Name):-
	( type_of_goal(exported,Goal)
	; type_of_goal(multifile,Goal)),
	functor(Goal,F,A),
	functor(G,F,A),
	\+ entry_assertion(G,_Call,_Name),
	make_atom([F,A],Name), % Name the unique topmost version of F/A
	varset(Goal,Qv),  
	( current_pp_flag(prog_lang,java) ->
	  unknown_entry(AbsInt,Goal,Qv,Call)
	;
	  unknown_entry(AbsInt,Qv,Call)
        ).
entry_point(AbsInt,Name,[],Call,Name):-
	setcounter(0,0),
	( type_of_directive(initialization,Body)
	; type_of_directive(on_abort,Body) ),
	inccounter(0,Name), % Name of directive a number
	varset(Body,Bv),
	vars_names_dict(Ds,Bv,_Ns),
	transform_clauses([(clause(Name,Body),Name)],Ds,[nr],[],AbsInt),
	empty_entry(AbsInt,[],Call).
entry_point(AbsInt,Goal,Qv,Call,Name):-
	setcounter(1,0),
	entry_assertion(Goal,CInfo,Name0),
	( functor(Goal,Name0,A)  % Name one possible entry version
	-> inccounter(1,C),
	   make_atom([Name0,A,C],Name)
	 ; Name=Name0
	),
%	get_unify(CInfo,CInfo0,Unif),
	varset(Goal,Qv),
%	varset((Goal,Unif),Qa),
	info_to_asub(AbsInt,_approx,CInfo,Qv,Call).
%	analyze_unify(Unif,AbsInt,Call0,Call).

     % get_unify([],[],[]).
% get_unify([U|CInfo],CInfo0,[U|Unif]):-
% 	functor(U,=,2),!,
% 	get_unify(CInfo,CInfo0,Unif).
% get_unify([I|CInfo],[I|CInfo0],Unif):-
% 	get_unify(CInfo,CInfo0,Unif).


% analyze_unify([],_AbsInt,Call,Call):- !.
% analyze_unify([U|Unif],AbsInt,Call0,Call):-
%         builtin_info(U,'=/2',AbsInt,T,_,Cv),
% 	varset(U,Sv),
% 	project(AbsInt,Sv,_,Call0,Proj),
% 	body_succ_builtin(T,AbsInt,U,Cv,Sv,_,Call0,Proj,Call1),!,
% 	analyze_unify(Unif,AbsInt,Call1,Call).
	

%------------------------------------------------------------------------%
:- doc(mod_plai(Cls,Ds,Fixp,AbsInt,Time),"Performs the 
	analysis of the clauses in @var{Cls} (dictionaries of
	variables in @var{Ds}) with fixpoint algorithm @var{Fixp} and
	analysis domain @var{AbsInt}.  The analysis results are left
	in a permanent database (see @lib{plai_db}).

	The analysis uses the entry information provided by the entry
	policy selected in entry_policy preprocessing flag.").

mod_plai(Cls,Ds,Fixp,AbsInt,[time(Total,[(prep,T1),(ana,T)])|Info]):-
	statistics(runtime,_),
	% initialization
	init_abstract_domain(AbsInt,Norm),
	init_fixpoint(Fixp),
	init_unfold,
	cleanup_clauses,
	undo_errors,
	preprocess(Fixp,Norm,AbsInt,Cls,Ds,Ps),
	statistics(runtime,[_,T1]),
	display_list(['{preprocessed for plai in ',T1, ' msec.}\n']),
	mod_topdown_analysis(AbsInt,Fixp,Ps),
	pop_pp_flag(variants),
	statistics(runtime,[_,T]),
	Total is T + T1,
	display_list(['{analyzed by ', Fixp, ' using ', AbsInt, ' in ', T,
	    ' msec.}\n']),
	dom_statistics(AbsInt, Info).

%------------------------------------------------------------------------%

mod_topdown_analysis(AbsInt,Fixp,Ps):-
	setcounter(1,0),
	get_entry_info(AbsInt,Goal,Call),
	varset(Goal,Gv),
	functor(Goal,F,A),
	inccounter(1,C),
	make_atom([F,A,C],Name),
	( analyze(Fixp,AbsInt,Ps,Goal,Gv,Call,Name) -> true ),
	fail.
mod_topdown_analysis(_AbsInt,_Fixp,_Ps).
